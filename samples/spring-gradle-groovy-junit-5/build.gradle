import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
	id 'org.springframework.boot' version '3.0.6'
	id 'io.spring.dependency-management' version '1.1.0'
	id 'org.jetbrains.kotlin.jvm' version '1.8.20'
	id 'org.jetbrains.kotlin.plugin.spring' version '1.8.20'
	id 'jvm-test-suite'
}

group = 'com.sample'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '19'

repositories {
	mavenCentral()
}

dependencies {
	// The 'api' is used, so Spring classes are available in the konsistTest source set
	api 'org.springframework.boot:spring-boot-starter'

	implementation 'org.jetbrains.kotlin:kotlin-reflect'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.withType(KotlinCompile) {
	kotlinOptions {
		freeCompilerArgs = ['-Xjsr305=strict']
		jvmTarget = '19'
	}
}

kotlin {
	jvmToolchain(19)
}

tasks.named('test') {
	useJUnitPlatform()
}

testing {
	suites {
		test {
			useJUnitJupiter()
		}

		konsistTest(JvmTestSuite) {
			dependencies {
				// Add 'main' source set dependency
				implementation project()

				// Add Konsist dependency
				implementation "com.lemonappdev:konsist:0.7.6"
			}

			targets {
				all {
					testTask.configure {
						shouldRunAfter(test)
					}
				}
			}
		}
	}
}

// Optional block to run Konsist tests together with the Gradle 'check' task
tasks.named('check') {
	dependsOn(testing.suites.konsistTest)
}
